C  XPLACE.F - EVALUATE FLUIDS IN PLACE AND PUT ACCUMUMLATION TERMS
C               IN MASS CONSERVATION RESIDUALS. WORK ROUTINE.

C  ROUTINES IN THIS MODULE:

C  SUBROUTINE XINPLACE1(IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
C                      KEYOUT,NBLK,CONC,CONCN,PV,PVN,RESID,
C                      CR,DFLOW)

C  SUBROUTINE XINPLACE2(IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,KL2,
C                      KEYOUT,NBLK,CONC,CONCN)

C  CODE HISTORY:

C  RICK DEAN   6/26/01   INITIAL VERSION

C*********************************************************************
      SUBROUTINE XINPLACE1(IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,
     &                     KL2,KEYOUT,NBLK,CONC,PV,CONCN,PVN,RESID,
     &                     CR,DFLOW)
C*********************************************************************
 

C  INPUT:
C     CONC(I,J,K,IC) = CONCENTRATION OF IC (REAL*8)
C     PV(I,J,K) = PORE VOLUME (REAL*8)
C     CONCN(I,J,K,IC) = CONCENTRATION OF IC AT START OF TIMESTEP(REAL*8)
C     PVN(I,J,K) = PORE VOLUME AT START OF TIMESTEP (REAL*8)
C     CR(I,J,K) = ROCK COMPRESSIBILITY X PV0 (REAL*8)

C  OUTPUT:
C     RESID(I,J,K,IC) = RESIDUAL FOR MASS CONSERVATION EQN IC (REAL*8)
C     CURRENT(IC) = TOTAL MASS OF COMPONENT IC (REAL*8)
C     DFLOW(I,J,K,IC) = DIAGONAL COEF FOR MASS CONSERVATION EQN IC (REAL*8)


C***********************************************************************
      IMPLICIT NONE
      INCLUDE 'control.h'
      INCLUDE 'xmodel.h'
      INCLUDE 'xparam.h'
      INCLUDE 'xbaldat.h'
C-----------------------------------------------------------------------
C     Dummy Arguments
C-----------------------------------------------------------------------
      INTEGER IDIM,JDIM,KDIM,IL1,IL2,KL1,KL2,NBLK,LDIM
      INTEGER JL1V(KDIM),JL2V(KDIM),KEYOUT(IDIM,JDIM,KDIM)
      REAL*4 CR(IDIM,JDIM,KDIM)
      REAL*8 CONC(IDIM,JDIM,KDIM,NC),RESID(IDIM,JDIM,KDIM,NC),  
     &       PV(IDIM,JDIM,KDIM),CONCN(IDIM,JDIM,KDIM,NC),
     &       PVN(IDIM,JDIM,KDIM),DFLOW(IDIM,JDIM,KDIM,NC)
C-----------------------------------------------------------------------
C     Local Variables
C-----------------------------------------------------------------------
      INTEGER I,J,K,JL1,JL2,IC
      REAL*8 U,X

      IF(NEWT == 1) THEN
         DO IC = 1,NC
            U = ZERO
            DO K = KL1,KL2
               JL1 = JL1V(K)
               JL2 = JL2V(K)
               DO  J = JL1,JL2
               DO  I = IL1,IL2
                  RESID(I,J,K,IC) = ZERO
                  IF (KEYOUT(I,J,K) > 0) THEN
                     DFLOW(I,J,K,IC) = CR(I,J,K)*CONC(I,J,K,IC)
                     U = U + PV(I,J,K)*CONC(I,J,K,IC)
                  ELSE
                     DFLOW(I,J,K,IC) = ZERO
                  ENDIF
               END DO
               END DO
            END DO
            CURRENT(IC) = CURRENT(IC) + U
         END DO
      ELSE
         DO IC = 1,NC
            U = ZERO
            DO K = KL1,KL2
               JL1 = JL1V(K)
               JL2 = JL2V(K)
               DO  J = JL1,JL2
               DO  I = IL1,IL2
                  IF (KEYOUT(I,J,K) > 0) THEN
                     X = PV(I,J,K)*CONC(I,J,K,IC)
                     DFLOW(I,J,K,IC) = CR(I,J,K)*CONC(I,J,K,IC)
                     RESID(I,J,K,IC) = X-PVN(I,J,K)*CONCN(I,J,K,IC)
                     U = U + X
                  ELSE
                     DFLOW(I,J,K,IC) = ZERO
                     RESID(I,J,K,IC) = ZERO
                  ENDIF
               END DO
               END DO
            END DO
            CURRENT(IC) = CURRENT(IC) + U
         END DO
      ENDIF
      END

C**********************************************************************
      SUBROUTINE XINPLACE2(IDIM,JDIM,KDIM,LDIM,IL1,IL2,JL1V,JL2V,KL1,
     &                     KL2,KEYOUT,NBLK,CONC,PV)
C**********************************************************************
C  INPUT:
C     CONC(I,J,K,IC) = CONCENTRATION OF IC (REAL*8)
C     PV(I,J,K) = PORE VOLUME (REAL*8)

C  OUTPUT:
C     CURRENT(IC) = TOTAL MASS OF COMPONENT IC (REAL*8)
C***********************************************************************
      IMPLICIT NONE
      INCLUDE 'control.h'
      INCLUDE 'xmodel.h'
      INCLUDE 'xparam.h'
      INCLUDE 'xbaldat.h'
C-----------------------------------------------------------------------
C     Dummy Arguments
C-----------------------------------------------------------------------
      INTEGER IDIM,JDIM,KDIM,IL1,IL2,KL1,KL2,NBLK,LDIM
      INTEGER JL1V(KDIM),JL2V(KDIM),KEYOUT(IDIM,JDIM,KDIM)
      REAL*8 CONC(IDIM,JDIM,KDIM,NC),PV(IDIM,JDIM,KDIM)
C-----------------------------------------------------------------------
C     Local Variables
C-----------------------------------------------------------------------
      INTEGER I,J,K,IC
      REAL*8 U

      DO IC = 1,NC
         U = ZERO
         DO K = KL1,KL2
            DO  J = JL1V(K),JL2V(K)
            DO  I = IL1,IL2
               IF (KEYOUT(I,J,K) > 0) THEN
                  U = U + PV(I,J,K)*CONC(I,J,K,IC)
               ENDIF
            END DO
            END DO
         END DO
         CURRENT(IC) = CURRENT(IC) + U
      END DO
      END
  
