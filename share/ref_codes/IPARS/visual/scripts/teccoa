#! /bin/csh
#
# TECCOA
# 
# C - shell utility to combine IPARS zone output for Tecplot in one
# file for ALL prcoessors, invokes TECCO, see the script for details
#
# it detects the recently created time steps, uses TECinfo.processor
# 
# assumptions : rootname is TEC, faultblock is 0, visflag is 3
#
# MP, 4/20/98
#
set rootname = 'TEC'
set infoname = 'TECinfo'
set faultblock = 0
set zonelist = ()
set number = 0
set visflag = 3
#
set nsteps = 0
set flist=()
#
foreach arg ($*)
	@ number ++
	switch ($number) 
	case 1:	
	 set nprocessor = $arg
	 breaksw
	endsw
end			
#		
# echo 'number of arguments is ' $number
#
if ( $number < 1 ) then
	echo 'Too few parameters to TECCOA.'
	echo "Usage: TECCOA  numprocs "
	exit 1
endif

#
##### process the files
#

set procnum = 0
while ( $procnum < $nprocessor )	
	#
	##### get the list of zones from the INFO file
	#
	set infofile = "$infoname.$procnum"
	####################### get all the time steps
	rm -f zonedum
	awk -F. ' NR > 2{ \
	printf("%s ", $NF);  \
	}' $infofile > zonedum
	set zonelist = `cat zonedum`
	set csteps = 0
	foreach zone ($zonelist)
		@ csteps ++	
	end	
#	echo "for proc $procnum counted $csteps $nsteps"
	if ( $procnum == 0 ) then
		set nsteps = $csteps
	else
		if ( $nsteps != $csteps )  then
		 echo "Warning: different number of time steps for $procnum"
		 goto endshell
		endif
		set nsteps = $csteps
	endif
#
# call tecco (and Preplot)
#
	echo "Calling tecco $rootname $faultblock $procnum $visflag $zonelist."
	tecco $rootname $faultblock $procnum $visflag $zonelist
#
# create the list of input files to be used by sed 
#
# bag8 - change mydir to macrofilepath
	set flist = ($flist \
	"|macrofilepath|/$rootname.$faultblock.$procnum.$visflag.plt")
#	echo "current flist= $flist"

	@ procnum++	
	echo "-----------------------------"	
end
#
#### reset the TECload, TECanim, TECfirst macros 
#### 
echo "Resetting MCR (Tecplot macro) files:" 
echo "Files dum,fdum,tmp will be destroyed. Proceed ? (y/n)"
set reply = $<
if ( $reply != "y" ) then
	goto endshell
endif
#
## create the sed script dum that applies to all src files 
#
rm -f dum
echo "4 s/=/=  $nprocessor/" > dum
echo "5 s/=/=  $nsteps/"  >> dum
#
## loop over the macro files: insert the current values of nprocessor, nsteps
#
foreach mfile(TECload TECfirst TECanim)
	echo "$mfile.mcr.."
	rm -f $mfile.mcr 	
	sed -f dum $mfile.src > $mfile.mcr
	echo "..has been created OK."
end
#
### special treatment of TECload.mcr: append filenames
## create the sed script fdum
#
rm -f fdum		
echo '8 a\' > fdum
echo "'"`pwd`"'" >> fdum
echo '9 a\' >> fdum
rm -f tmp
mv TECload.mcr tmp
echo '"'"$flist"'"' >> fdum
sed -f fdum tmp > TECload.mcr
#### 
##############
echo "TECCOA ended OK."
exit 0
### we are here if error occured or user broke themacro
endshell:
echo "TECCOA interrupted."
exit 1
